apply plugin: 'com.android.library'
//配置插件
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.3"

    defaultConfig {
        minSdkVersion 18
        targetSdkVersion 25
        versionCode 21
        versionName "2.0.4"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        abortOnError false
//        disable 'MissingTranslation'
//        ignoreWarnings true
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile 'com.android.support:appcompat-v7:25.3.1'
    testCompile 'junit:junit:4.12'
}
tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

version = "2.0.4"       //这个是版本号，必须填写
def siteUrl = 'https://github.com/Alex-Jerry/BleDemo'      // 项目的主页   这个是说明，可随便填
def gitUrl = 'https://github.com/Alex-Jerry/BleDemo'      // Git仓库的url  这个是说明，可随便填
//compile 'com.arshowbaby:mylibrary:1.0.0'中的com.arshowbaby
group = "cn.com.superLei"    // 这里是groupId ,必须填写  一般填你唯一的包名

install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom {
            project {
                packaging 'aar'
                // 项目描述，复制我的话，这里需要修改。
                name 'optimized part of the interface,added OTA upgrade interface'     //项目描述
                url siteUrl
                // 软件开源协议，现在一般都是Apache License2.0吧，复制我的，这里不需要修改。
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                //填写开发者基本信息，复制我的，这里需要修改。
                developers {
                    developer {
                        id 'liulei'        //填写开发者的一些基本信息
                        name 'liulei'    //填写开发者的一些基本信息
                        email 'superliu0911@gmail.com'   //填写开发者的一些基本信息
                    }
                }
                // SCM，复制我的，这里不需要修改。
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }
            }
        }
    }
}
// 生成jar包的task，不需要修改。
task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}
// 生成jarDoc的task，不需要修改。
task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    options.encoding = "UTF-8"
}
// 生成javaDoc的jar，不需要修改
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
artifacts {
    archives javadocJar
    archives sourcesJar
}
// 这里是读取Bintray相关的信息，我们上传项目到github上的时候会把gradle文件传上去，
// 所以不要把帐号密码的信息直接写在这里，写在local.properties中，这里动态读取。
Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
bintray {
    //读取 local.properties 文件里面的 bintray.user
    user = properties.getProperty("bintray.user")
    //读取 local.properties 文件里面的 bintray.apikey
    key = properties.getProperty("bintray.apikey")

    configurations = ['archives']
    pkg {
        userOrg = "superliu"      //发布到JCenter的组织，注意新版本的bintray是需要手动创建的
        repo = "maven"          //发布到JCenter上的仓库名称，注意新版本的bintray是需要手动创建的
        // 发布到Bintray上的项目名字，这里的名字不是compile 'com.arshowbaby:mylibrary:1.0.0'中的mylibrary
        name = "BleLib"
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ["Apache-2.0"]
        publish = true  // 是否是公开项目
    }
}
//apply from: 'install.gradle'
//apply from: 'bintray.gradle'
//https://oss.sonatype.org/content/repositories/releases/com/junkchen/blelib
//Android Studio 发布库到jcenter步骤：
// 1、Gradle->other->install
// 2、将build/poms/下的 pom-default.xml 文件复制到 build/libs/中，并修改其后缀名为 blelib-1.0.0.pom
// 3、将build/outputs/aar/下的 blelib-release.aar 文件复制到 build/libs/中，更名如：blelib-1.0.0.aar
// 4、Gradle->publishing->bintrayUpload


//apply from: 'https://raw.githubusercontent.com/nuuneoi/JCenter/master/installv1.gradle'
//apply from: 'https://raw.githubusercontent.com/nuuneoi/JCenter/master/bintrayv1.gradle'
